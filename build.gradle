plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "maven-publish"
}

ext {
    versions = [
            "logging": "0.3",
    ]
}

group = "tk.frostbit"
version = "0.1"

repositories {
    mavenCentral()
    google()
    maven {
        url "https://jitpack.io"
        content {
            includeModule("com.github.kinfra", "kinfra-logging")
        }
    }
    mavenLocal()
}

dependencies {
    api "com.github.kinfra:kinfra-logging:${versions.logging}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
}

android {
    compileSdkVersion = 30
    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 30
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs += [
                "-Xjvm-default=all"
        ]
    }
}

task androidSourcesJar(type: Jar) {
    archiveClassifier = "sources"
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        maven(MavenPublication) {
            afterEvaluate {
                from components.release
            }
            artifact androidSourcesJar
        }
    }
}
